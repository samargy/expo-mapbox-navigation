apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'expo.modules.mapboxnavigation'
version = '0.1.0'

buildscript {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
  }

  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  // Ensures backward compatibility
  ext.getKotlinVersion = {
    if (ext.has("kotlinVersion")) {
      ext.kotlinVersion()
    } else {
      ext.safeExtGet("kotlinVersion", "1.8.10")
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getKotlinVersion()}")
  }
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
      }
    }
    repositories {
      maven {
        url = mavenLocal().url
      }
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 34)

  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
  if (agpVersion.tokenize('.')[0].toInteger() < 8) {
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_11
      targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_11.majorVersion
    }
  }

  namespace "expo.modules.mapboxnavigation"
  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 34)
    versionCode 1
    versionName "0.1.0"
  }
  lintOptions {
    abortOnError false
  }
  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  def lifecycle_version = "2.8.1"
  implementation project(':expo-modules-core')
  implementation "com.mapbox.navigationcore:android:3.8.0"
  implementation "com.mapbox.navigationcore:ui-components:3.8.0"
  implementation "com.google.android.gms:play-services-location:21.0.1"
  implementation "androidx.constraintlayout:constraintlayout:2.2.0-alpha13"
  
  // Always force ndk27 variants to override transitive dependencies from Navigation SDK
  // This ensures compatibility with @rnmapbox/maps which uses ndk27 for modern Android SDKs
  implementation "com.mapbox.maps:android-ndk27:11.11.0"
  implementation "com.mapbox.maps:base-ndk27:11.11.0"
  implementation "com.mapbox.common:common-ndk27:24.11.0"
  implementation "com.mapbox.plugin:maps-gestures-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-lifecycle-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-locationcomponent-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-logo-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-overlay-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-scalebar-ndk27:11.11.0"
  implementation "com.mapbox.plugin:maps-viewport-ndk27:11.11.0"
  implementation "com.mapbox.module:maps-telemetry-ndk27:11.11.0"
}
